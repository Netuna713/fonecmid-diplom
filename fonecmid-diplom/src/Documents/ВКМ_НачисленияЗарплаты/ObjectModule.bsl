#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Для Каждого Строка Из Начисления Цикл
		
		Если Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ФиксированнаяПремия Тогда
			ОбщегоНазначения.СообщитьПользователю("Невозможно в данном документе начислить фиксированную премию");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
	КонецЦикла;
	
	СформироватьДвиженияПоОтпуску();
	РассчитатьОтпуск();
	СформироватьДвиженияПоОкладу();
	РассчитатьОклад();
	СформироватьДвиженияПоОплатеПроцентом();
	СформироватьДвиженияУдержания();
	СформироватьДвиженияВзаиморасчетыССотрудниками();
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвиженияПоОкладу()
	
	Для Каждого Строка Из Начисления Цикл
		
		Если ТипЗнч(Строка.ВидРасчета) = Тип("ПланВидовРасчетаСсылка.ВКМ_ОсновныеНачисления") Тогда
			
			Если Строка.ВидРасчета <> ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад Тогда
				Продолжить;
			КонецЕсли;
			
			Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
			Движение.ПериодРегистрации = Дата;
			Движение.ВидРасчета = Строка.ВидРасчета;
			Движение.ПериодДействияНачало = Строка.ДатаНачала;
			Движение.ПериодДействияКонец = Строка.ДатаОкончания;
			Движение.Сотрудник = Строка.Сотрудник;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать();

КонецПроцедуры

Процедура СформироватьДвиженияПоОтпуску()
		
	Для Каждого Строка Из Начисления Цикл
	
		Если ТипЗнч(Строка.ВидРасчета) = Тип("ПланВидовРасчетаСсылка.ВКМ_ОсновныеНачисления") Тогда

			Если Строка.ВидРасчета <> ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда
				Продолжить;
			КонецЕсли;
				
			Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
			Движение.ПериодРегистрации = Дата;
			Движение.ВидРасчета = Строка.ВидРасчета;
			Движение.ПериодДействияНачало = Строка.ДатаНачала;
			Движение.ПериодДействияКонец = Строка.ДатаОкончания;
			Движение.Сотрудник = Строка.Сотрудник;
			Движение.БазовыйПериодНачало = НачалоМесяца(ДобавитьМесяц(Строка.ДатаНачала, -12));
			Движение.БазовыйПериодКонец = КонецМесяца(ДобавитьМесяц(Строка.ДатаОкончания, -1));
		
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать();

КонецПроцедуры

Процедура СформироватьДвиженияПоОплатеПроцентом()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_НачисленияЗарплатыНачисления.Сотрудник КАК Сотрудник,
		|	ВКМ_НачисленияЗарплатыНачисления.ВидРасчета КАК ВидРасчета,
		|	ВКМ_НачисленияЗарплатыНачисления.ДатаНачала КАК ДатаНачала,
		|	ВКМ_НачисленияЗарплатыНачисления.ДатаОкончания КАК ДатаОкончания,
		|	ВКМ_ВыполненныеСотрудникомРаботыОбороты.СуммаКОплатеОборот КАК СуммаКОплате
		|ИЗ
		|	Документ.ВКМ_НачисленияЗарплаты.Начисления КАК ВКМ_НачисленияЗарплатыНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(&ДатаНачала, &ДатаКонец,, Сотрудник В
		|			(ВЫБРАТЬ
		|				ВКМ_НачисленияЗарплатыНачисления.Сотрудник КАК Сотрудник
		|			ИЗ
		|				Документ.ВКМ_НачисленияЗарплаты.Начисления КАК ВКМ_НачисленияЗарплатыНачисления)) КАК
		|			ВКМ_ВыполненныеСотрудникомРаботыОбороты
		|		ПО ВКМ_НачисленияЗарплатыНачисления.Сотрудник = ВКМ_ВыполненныеСотрудникомРаботыОбороты.Сотрудник
		|ГДЕ
		|	ВКМ_НачисленияЗарплатыНачисления.Ссылка = &Ссылка
		|	И ВКМ_НачисленияЗарплатыНачисления.ВидРасчета = &ВидРасчета";
	
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.РасчетЗарплатыПроцентом);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("ДатаКонец", КонецМесяца(Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если ТипЗнч(Выборка.ВидРасчета) = Тип("ПланВидовРасчетаСсылка.ВКМ_ДополнительныеНачисления") Тогда
			
			Если Выборка.ВидРасчета <> ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.РасчетЗарплатыПроцентом Тогда
				Продолжить;
			КонецЕсли;
			
			Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
			Движение.ПериодРегистрации = Дата;
			Движение.ВидРасчета = Выборка.ВидРасчета;
			Движение.Сотрудник = Выборка.Сотрудник;
			Движение.Сумма = Выборка.СуммаКОплате;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.ВКМ_ДополнительныеНачисления.Записать();
	
КонецПроцедуры	

Процедура СформироватьДвиженияУдержания();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	НачисленияДокумента.Сотрудник КАК Сотрудник,
		|	НачисленияДокумента.ВидРасчета КАК ВидРасчета,
		|	СУММА(ЕСТЬNULL(ВКМ_ОсновныеНачисления.Сумма, 0)) КАК СуммаОсновных
		|ПОМЕСТИТЬ втОсновные
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ВКМ_НачисленияЗарплатыНачисления.Сотрудник КАК Сотрудник,
		|		ВКМ_НачисленияЗарплатыНачисления.ВидРасчета КАК ВидРасчета
		|	ИЗ
		|		Документ.ВКМ_НачисленияЗарплаты.Начисления КАК ВКМ_НачисленияЗарплатыНачисления
		|	ГДЕ
		|		ВКМ_НачисленияЗарплатыНачисления.Ссылка = &Ссылка) КАК НачисленияДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|		ПО НачисленияДокумента.Сотрудник = ВКМ_ОсновныеНачисления.Сотрудник
		|		И НачисленияДокумента.ВидРасчета = ВКМ_ОсновныеНачисления.ВидРасчета
		|		И НАЧАЛОПЕРИОДА(ВКМ_ОсновныеНачисления.ПериодДействия, МЕСЯЦ) = &Период
		|ГДЕ
		|	ВКМ_ОсновныеНачисления.ПериодДействия <= КОНЕЦПЕРИОДА(&Период, ДЕНЬ)
		|СГРУППИРОВАТЬ ПО
		|	НачисленияДокумента.Сотрудник,
		|	НачисленияДокумента.ВидРасчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленияДокумента.Сотрудник КАК Сотрудник,
		|	НачисленияДокумента.ВидРасчета КАК ВидРасчета,
		|	СУММА(ЕСТЬNULL(ВКМ_ДополнительныеНачисления.Сумма, 0)) КАК СуммаДополнительных
		|ПОМЕСТИТЬ втДополнительные
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ВКМ_НачисленияЗарплатыНачисления.Сотрудник КАК Сотрудник,
		|		ВКМ_НачисленияЗарплатыНачисления.ВидРасчета КАК ВидРасчета
		|	ИЗ
		|		Документ.ВКМ_НачисленияЗарплаты.Начисления КАК ВКМ_НачисленияЗарплатыНачисления
		|	ГДЕ
		|		ВКМ_НачисленияЗарплатыНачисления.Ссылка = &Ссылка) КАК НачисленияДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
		|		ПО НачисленияДокумента.Сотрудник = ВКМ_ДополнительныеНачисления.Сотрудник
		|		И НачисленияДокумента.ВидРасчета = ВКМ_ДополнительныеНачисления.ВидРасчета
		|		И ВКМ_ДополнительныеНачисления.ПериодРегистрации = &Период
		|СГРУППИРОВАТЬ ПО
		|	НачисленияДокумента.Сотрудник,
		|	НачисленияДокумента.ВидРасчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(Основные.Сотрудник, Дополнительные.Сотрудник) КАК Сотрудник,
		|	ЕСТЬNULL(Основные.ВидРасчета, Дополнительные.ВидРасчета) КАК ВидРасчета,
		|	ЕСТЬNULL(Основные.СуммаОсновных, 0) + ЕСТЬNULL(Дополнительные.СуммаДополнительных, 0) КАК Сумма
		|ИЗ
		|	втОсновные КАК Основные
		|		ПОЛНОЕ СОЕДИНЕНИЕ втДополнительные КАК Дополнительные
		|		ПО Основные.Сотрудник = Дополнительные.Сотрудник
		|		И Основные.ВидРасчета = Дополнительные.ВидРасчета";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", НачалоМесяца(Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_Удержания.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.Сумма = Выборка.Сумма * 13 / 100;
		
	КонецЦикла;
	
	Движения.ВКМ_Удержания.Записать();
	
КонецПроцедуры

Процедура РассчитатьОклад()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.РабочийДеньФактическийПериодДействия КАК Факт,
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.РабочийДеньПериодДействия КАК План,
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад КАК Оклад
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Регистратор = &Ссылка
		|	И ВидРасчета = &Оклад) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Период,) КАК
		|			ВКМ_УсловияОплатыСотрудниковСрезПоследних
		|		ПО ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.ВКМ_ОсновныеНачисления[ВыборкаДетальныеЗаписи.НомерСтроки - 1];
		Движение.Сумма = ВыборкаДетальныеЗаписи.Оклад / ВыборкаДетальныеЗаписи.План * ВыборкаДетальныеЗаписи.Факт;
		Движение.КоличествоДней = ВыборкаДетальныеЗаписи.Факт;
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать();
	
КонецПроцедуры

Процедура РассчитатьОтпуск()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.СуммаБаза, 0) КАК БазаОсн,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.СуммаБаза, 0) КАК БазаДоп,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.РабочийДеньФактическийПериодДействия, 0) КАК Факт,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.КоличествоДнейБаза, 0) КАК КоличествоДнейБаза
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(
	|				&Измерения,
	|				&Измерения,
	|				,
	|				Регистратор = &Ссылка
	|					И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления
	|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ДополнительныеНачисления(
	|				&Измерения,
	|				&Измерения,
	|				,
	|				Регистратор = &Ссылка
	|					И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления
	|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
	|				Регистратор = &Ссылка
	|					И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
	|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки
	|ГДЕ
	|	ВКМ_ОсновныеНачисления.Регистратор = &Ссылка
	|	И ВКМ_ОсновныеНачисления.ВидРасчета = &Отпуск";
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("Отпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки - 1];
		
		Движение.КоличествоДней = Выборка.Факт;
		
		Если Выборка.КоличествоДнейБаза = 0 Тогда
			Движение.Сумма = 0;
			Продолжить;
		КонецЕсли;
		
		Движение.Сумма = (Выборка.БазаОсн + Выборка.БазаДоп) * Выборка.Факт / Выборка.КоличествоДнейБаза;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьДвиженияВзаиморасчетыССотрудниками()
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СотрудникиДокумента.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(НачисленияСумма.Сумма, 0) КАК Сумма
		|ПОМЕСТИТЬ ВТ_Осн
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ВКМ_НачисленияЗарплатыНачисления.Сотрудник КАК Сотрудник
		|	ИЗ
		|		Документ.ВКМ_НачисленияЗарплаты.Начисления КАК ВКМ_НачисленияЗарплатыНачисления
		|	ГДЕ
		|		ВКМ_НачисленияЗарплатыНачисления.Ссылка = &Ссылка) КАК СотрудникиДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
		|			СУММА(ВКМ_ОсновныеНачисления.Сумма) КАК Сумма
		|		ИЗ
		|			РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|		ГДЕ
		|			ВКМ_ОсновныеНачисления.Регистратор = &Ссылка
		|		СГРУППИРОВАТЬ ПО
		|			ВКМ_ОсновныеНачисления.Сотрудник) КАК НачисленияСумма
		|		ПО СотрудникиДокумента.Сотрудник = НачисленияСумма.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВКМ_Удержания.Сумма) КАК Сумма,
		|	ВКМ_Удержания.Сотрудник КАК Сотрудник,
		|	ВКМ_Удержания.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТ_Удер
		|ИЗ
		|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
		|ГДЕ
		|	ВКМ_Удержания.Регистратор = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_Удержания.Сотрудник,
		|	ВКМ_Удержания.Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Осн.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ВТ_Осн.Сумма, 0) КАК ОсновнаяСумма,
		|	СУММА(ЕСТЬNULL(ВКМ_ДополнительныеНачисления.Сумма, 0)) КАК ДопСумма,
		|	СУММА(ЕСТЬNULL(ВТ_Удер.Сумма, 0)) КАК Удержано
		|ИЗ
		|	ВТ_Осн КАК ВТ_Осн
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
		|		ПО ВТ_Осн.Сотрудник = ВКМ_ДополнительныеНачисления.Сотрудник
		|		И ВКМ_ДополнительныеНачисления.Регистратор = &Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Удер КАК ВТ_Удер
		|		ПО ВТ_Осн.Сотрудник = ВТ_Удер.Сотрудник
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Осн.Сотрудник,
		|	ВТ_Осн.Сумма,
		|	ЕСТЬNULL(ВТ_Осн.Сумма, 0)";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.Сумма = Выборка.ОсновнаяСумма + Выборка.ДопСумма - Выборка.Удержано;
		
	КонецЦикла;
	
КонецПроцедуры 

#КонецОбласти

#КонецЕсли