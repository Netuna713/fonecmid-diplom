#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДиаграммаГанта.ОтображатьЛегенду = Ложь;
	ДанныеОтпусков = ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище);
	Объект.Год = Параметры.Год;
	ЗаполнитьДиаграммуГанта(ДанныеОтпусков, Параметры.Год);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Автозаголовок = Ложь;
	Заголовок = "График запланированных отпусков на " + Формат(Объект.Год, "ЧГ=") + " год";
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДиаграммуГанта(ДанныеОтпусков, Год)
	
	Диаграмма = ДиаграммаГанта;
	
	Диаграмма.Очистить();
	Диаграмма.АвтоОпределениеПолногоИнтервала = Ложь;
	
	НачалоГода = Дата(Год, 1, 1);
	КонецГода = КонецГода(Дата(Год, 12, 31));
	Диаграмма.УстановитьПолныйИнтервал(НачалоГода, КонецГода);
	
	ЭлементыШкалы = ДиаграммаГанта.ОбластьПостроения.ШкалаВремени.Элементы;
	ЭлементМесяц = ЭлементыШкалы.Добавить();
	ЭлементМесяц.Единица = ТипЕдиницыШкалыВремени.Месяц;
	ЭлементМесяц.Формат = "ДФ='MMMM'";
	
	СерияОтпуска = Диаграмма.УстановитьСерию("Плановые отпуска");
	
	СоотвТочек = Новый Соответствие;
	
	Для Каждого СтрокаОтпуска Из ДанныеОтпусков Цикл
		
		Сотрудник = СтрокаОтпуска.Сотрудник;
		
		Если СоотвТочек.Получить(Сотрудник) = Неопределено Тогда
			ТочкаСотрудника = Диаграмма.УстановитьТочку(Сотрудник);
			ТочкаСотрудника.Текст = Строка(Сотрудник);
			СоотвТочек.Вставить(Сотрудник, ТочкаСотрудника);
		Иначе
			ТочкаСотрудника = СоотвТочек.Получить(Сотрудник);
		КонецЕсли;
		
		ЗначениеТочки = Диаграмма.ПолучитьЗначение(ТочкаСотрудника, СерияОтпуска);
		Интервал = ЗначениеТочки.Добавить();
		
		Интервал.Начало = СтрокаОтпуска.ДатаНачала;
		Интервал.Конец = СтрокаОтпуска.ДатаОкончания;
		Интервал.Цвет = WebЦвета.СветлоЗеленый;
		
	КонецЦикла;
	
	Диаграмма.Обновление = Истина;
	
КонецПроцедуры

#КонецОбласти