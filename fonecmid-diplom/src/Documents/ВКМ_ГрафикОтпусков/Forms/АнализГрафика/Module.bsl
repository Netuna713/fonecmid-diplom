#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДанныеОтпусков = ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище);
	ЗаполнитьДиаграммуГанта(ДанныеОтпусков, Параметры.Год);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДиаграммуГанта(ДанныеОтпусков, Год)
		
	Диаграмма = ДиаграммаГанта;
	
	Диаграмма.Очистить();
	Диаграмма.ОбластьЗаголовка.Текст = "Запланированные отпуска на " + Год;
	Диаграмма.АвтоОпределениеПолногоИнтервала = Ложь;
	
	НачалоГода = Дата(Год, 1, 1);
	КонецГода = КонецГода(Дата(Год, 12, 31));
	Диаграмма.УстановитьПолныйИнтервал(НачалоГода, КонецГода);
	
	ЭлементыШкалы = ДиаграммаГанта.ОбластьПостроения.ШкалаВремени.Элементы;
	ЭлементМесяц = ЭлементыШкалы.Добавить();
	ЭлементМесяц.Единица = ТипЕдиницыШкалыВремени.Месяц;
	ЭлементМесяц.Формат = "ДФ='MMMM'";

	СерияОтпуска = Диаграмма.УстановитьСерию("Текущий документ");
	
	ДанныеОтпусков = ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище);
	СоотвТочек = Новый Соответствие;
	
	Для Каждого СтрокаОтпуска Из ДанныеОтпусков Цикл
	    Сотрудник = СтрокаОтпуска.Сотрудник;
	    
	    Если СоотвТочек.Получить(Сотрудник) = Неопределено Тогда
	        ТочкаСотрудника = Диаграмма.УстановитьТочку(СтрокаОтпуска.Сотрудник);
	        ТочкаСотрудника.Текст = Строка(Сотрудник);
	        СоотвТочек.Вставить(Сотрудник, ТочкаСотрудника);
	    Иначе
	        ТочкаСотрудника = СоотвТочек.Получить(Сотрудник);
	    КонецЕсли;
	    
	    ЗначениеТочки = Диаграмма.ПолучитьЗначение(ТочкаСотрудника, СерияОтпуска);
	    Интервал = ЗначениеТочки.Добавить();
	    
	    Интервал.Начало = СтрокаОтпуска.ДатаНачала;
	    Интервал.Конец = СтрокаОтпуска.ДатаОкончания;
	    Интервал.Текст = Формат(СтрокаОтпуска.ДатаНачала, "ДФ=dd.MM") + " - " + 
	                Формат(СтрокаОтпуска.ДатаОкончания, "ДФ=dd.MM");
	    Интервал.Расшифровка = СтрокаОтпуска.Документ;
	    Интервал.Цвет = WebЦвета.СветлоЗеленый;
	КонецЦикла;
	
	Диаграмма.Обновление = Истина;
	
КонецПроцедуры

#КонецОбласти