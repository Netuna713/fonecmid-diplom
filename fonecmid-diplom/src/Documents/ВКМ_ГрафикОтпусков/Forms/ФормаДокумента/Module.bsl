#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    СоздатьУсловноеОформление();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГодПриИзменении(Элемент)
	
	Если СтрДлина(Объект.Год) < 4 Тогда
	
		ОбщегоНазначенияКлиент.СообщитьПользователю("Неверно заполнено поле Год, необходимо ввести 4 цифры");	
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтпускаСотрудников

&НаКлиенте
Процедура ОтпускаСотрудниковДатаНачалаПриИзменении(Элемент)

    ОбновитьПодсветку();
    	
КонецПроцедуры

&НаКлиенте
Процедура ОтпускаСотрудниковДатаОкончанияПриИзменении(Элемент)

    ОбновитьПодсветку();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура АнализГрафика(Команда)
	
	АдресДанных  = ПодготовитьДанныеОтпусков(Объект.Год);
	
	ПараметрыФормы = Новый Структура("АдресВХранилище, Год", АдресДанных, Объект.Год);
	
	ОткрытьФорму("Документ.ВКМ_ГрафикОтпусков.Форма.АнализГрафика", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере

Функция ПодготовитьДанныеОтпусков(Год)

	ДанныеОтпусков = Новый ТаблицаЗначений;
	ДанныеОтпусков.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.ВКМ_Сотрудники"));
	ДанныеОтпусков.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
	ДанныеОтпусков.Колонки.Добавить("ДатаОкончания", Новый ОписаниеТипов("Дата"));
	ДанныеОтпусков.Колонки.Добавить("Документ", Новый ОписаниеТипов("ДокументСсылка.ВКМ_ГрафикОтпусков")); 

	Для Каждого СтрокаТЧ Из Объект.ОтпускаСотрудников Цикл

		Если Год(СтрокаТЧ.ДатаНачала) = Год Тогда
            НоваяСтрока = ДанныеОтпусков.Добавить();
            НоваяСтрока.Сотрудник = СтрокаТЧ.Сотрудник;
            НоваяСтрока.ДатаНачала = СтрокаТЧ.ДатаНачала;
            НоваяСтрока.ДатаОкончания = СтрокаТЧ.ДатаОкончания;
            НоваяСтрока.Документ = Объект.Ссылка;
		КонецЕсли;
 
	КонецЦикла;
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеОтпусков, УникальныйИдентификатор);
	
КонецФункции 

&НаКлиенте
Процедура ОбновитьПодсветку()
	
	ТекущаяСтрока = Элементы.ОтпускаСотрудников.ТекущиеДанные;
	
	ДлительностиОтпусков = 0;
    
	Для Каждого СтрокаОтпуска Из Объект.ОтпускаСотрудников Цикл
    	
		Если СтрокаОтпуска.Сотрудник = ТекущаяСтрока.Сотрудник Тогда
   
			КоличествоДней = (СтрокаОтпуска.ДатаОкончания - СтрокаОтпуска.ДатаНачала) / 86400 + 1;

			ДлительностиОтпусков = ДлительностиОтпусков + КоличествоДней;

		КонецЕсли;
        
	КонецЦикла;
    
    Для Каждого СтрокаТЧ Из Объект.ОтпускаСотрудников Цикл
    	
		Если СтрокаТЧ.Сотрудник = ТекущаяСтрока.Сотрудник Тогда
            СтрокаТЧ.Превышение = (ДлительностиОтпусков > 28);
		КонецЕсли;
        
    КонецЦикла;

	Элементы.ОтпускаСотрудников.Обновить();
    
КонецПроцедуры

&НаСервере
Процедура СоздатьУсловноеОформление()
    
    УсловноеОформление.Элементы.Очистить();
    
    ЭлементУО = УсловноеОформление.Элементы.Добавить();
    ЭлементУО.Использование = Истина;
    
    ОтборЭлемента = ЭлементУО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
    ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОтпускаСотрудников.Превышение");
    ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
    ОтборЭлемента.ПравоеЗначение = Истина;
    
    Поле = ЭлементУО.Поля.Элементы.Добавить();
    Поле.Поле = Новый ПолеКомпоновкиДанных("ОтпускаСотрудников");
    
    ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветФона", WebЦвета.БледноЛиловый);
    
КонецПроцедуры

#КонецОбласти