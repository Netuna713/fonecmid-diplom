
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Выплаты.Очистить();
	ОчиститьДвиженияВзаиморасчетыССотрудниками();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВыплаты

&НаКлиенте
Процедура ВыплатыСотрудникПриИзменении(Элемент)
	
	ТекСтрока = Элементы.Выплаты.ТекущиеДанные;
    
    Если ТекСтрока = Неопределено Тогда
        Возврат;
    КонецЕсли;
    
    ИдентификаторСтроки = ТекСтрока.ПолучитьИдентификатор();
    Сумма = ВыплатыСотрудникПриИзмененииНаСервере(ИдентификаторСтроки);
    
    ТекСтрока.Сумма = Сумма;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	Объект.Выплаты.Очистить();
	ЗаполнитьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВыплатыСотрудникПриИзмененииНаСервере(ИдентификаторСтроки)
	
	ОчиститьДвиженияВзаиморасчетыССотрудниками();
	
	СтрокаТЧ = Объект.Выплаты.НайтиПоИдентификатору(ИдентификаторСтроки);
    Если СтрокаТЧ = Неопределено Или Не ЗначениеЗаполнено(СтрокаТЧ.Сотрудник) Тогда
        Возврат 0;
    КонецЕсли;
    
    Запрос = Новый Запрос;
    Запрос.Текст = 
    "ВЫБРАТЬ
    |    ВКМ_ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток КАК СуммаОстаток
    |ИЗ
    |    РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками.Остатки(&Дата, Сотрудник = &Сотрудник) КАК ВКМ_ВзаиморасчетыССотрудникамиОстатки";
    
    Запрос.УстановитьПараметр("Дата", КонецДня(Объект.Дата));
    Запрос.УстановитьПараметр("Сотрудник", СтрокаТЧ.Сотрудник);
    
    РезультатЗапроса = Запрос.Выполнить();
    
    Выборка = РезультатЗапроса.Выбрать();
    Если Выборка.Следующий() Тогда
        Возврат Выборка.СуммаОстаток;
    Иначе
        Возврат 0;
    КонецЕсли;

КонецФункции

&НаСервере
Процедура ОчиститьДвиженияВзаиморасчетыССотрудниками()
	
	НаборЗаписей = РегистрыНакопления.ВКМ_ВзаиморасчетыССотрудниками.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Объект.Ссылка);
	НаборЗаписей.Записать();	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	ОчиститьДвиженияВзаиморасчетыССотрудниками();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ВзаиморасчетыССотрудникамиОстатки.Сотрудник КАК Сотрудник,
	|	ВКМ_ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток КАК СуммаОстаток
	|ИЗ
	|	РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками.Остатки(&Дата, ) КАК ВКМ_ВзаиморасчетыССотрудникамиОстатки";
	
	Запрос.УстановитьПараметр("Дата", КонецДня(Объект.Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Объект.Выплаты.Добавить();
		НоваяСтрока.Сотрудник = Выборка.Сотрудник;
		НоваяСтрока.Сумма = Выборка.СуммаОстаток;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

