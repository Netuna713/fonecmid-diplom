 
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.ВКМ_ДатаНачалаДействия КАК ДатаНачалаДействия,
		|	ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДействия КАК ДатаОкончанияДействия,
		|	ДоговорыКонтрагентов.ВидДоговора КАК ВидДоговора
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.Ссылка = &Договор";
	
	Запрос.УстановитьПараметр("Договор", Договор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбоненскоеОбслуживание Тогда
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю("Необходимо выбрать договор абоненского обслуживания");
			Возврат;
		КонецЕсли;
			
		Если Дата > КонецДня(Выборка.ДатаОкончанияДействия) Тогда
		
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю("Дата документа позже даты действия договора");
			
		ИначеЕсли Дата < Выборка.ДатаНачалаДействия Тогда
			
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю("Дата документа раньше даты действия договора");
		
		КонецЕсли;	
				
	КонецЦикла;
 
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СтоимостьЧасаРаботы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Договор.ВКМ_СтоимостьЧасаРаботы");
	
	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записать();
	
	Для Каждого ТекСтрокаВыполненныеРаботы из ВыполненныеРаботы Цикл
		
		Если ТекСтрокаВыполненныеРаботы.ЧасыКОплатеКлиенту = 0 Тогда
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон("В строке %1 не заполнено поле ""Часы к оплате клиенту""", 
			ТекСтрокаВыполненныеРаботы.НомерСтроки));
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Клиент = Клиент;
		Движение.Договор = Договор;
		Движение.КоличествоЧасов = ТекСтрокаВыполненныеРаботы.ЧасыКОплатеКлиенту;
		Движение.СуммаКОплате = СтоимостьЧасаРаботы * ТекСтрокаВыполненныеРаботы.ЧасыКОплатеКлиенту;
		
	КонецЦикла;
	
	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Специалист КАК Специалист,
		|	ЕСТЬNULL(ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.ЧасыКОплатеКлиенту, 0) КАК ЧасыКОплатеКлиенту,
		|	ДоговорыКонтрагентов.ВКМ_СтоимостьЧасаРаботы КАК СтоимостьЧасаРаботы,
		|	ЕСТЬNULL(ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот, ""НетСтавки"") КАК ПроцентОтРабот
		|ИЗ
		|	Документ.ВКМ_ОбслуживаниеКлиента.ВыполненныеРаботы КАК ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Период,) КАК
		|			ВКМ_УсловияОплатыСотрудниковСрезПоследних
		|		ПО ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Специалист = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|		ПО ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор = ДоговорыКонтрагентов.Ссылка
		|ГДЕ
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Сотрудник = Выборка.Специалист;
		Движение.ЧасовКОплате = Выборка.ЧасыКОплатеКлиенту;
		
		Если Выборка.ПроцентОтРабот = "НетСтавки" Тогда
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Специалисту %1 не установлен процент оплаты от работ", Выборка.Специалист));
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Движение.СуммаКОплате = Выборка.ЧасыКОплатеКлиенту * Выборка.ПроцентОтРабот * Выборка.СтоимостьЧасаРаботы /100;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
