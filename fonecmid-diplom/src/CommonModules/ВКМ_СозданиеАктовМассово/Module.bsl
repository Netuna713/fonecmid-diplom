#Область СлужебныеПроцедурыИФункции

Функция ЗаполнитьИСоздатьРеализации(Период) Экспорт
	
	ДатаНачала = Период.ДатаНачала; 
	ДатаОкончания = Период.ДатаОкончания;
	
	ПерезаполнитьРеализации(ДатаНачала, ДатаОкончания);
	
	Результат = СозданиеНовыхРеализаций(ДатаНачала, ДатаОкончания);
	
	Возврат Результат;
	
КонецФункции

Функция СозданиеНовыхРеализаций(ДатаНачала, ДатаОкончания)
	
	Перем Выборка, ДокументЗаполнен, Запрос, КоличествоДокументов, НовыйДокумент, РезультатЗапроса;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Договор,
	|	ДоговорыКонтрагентов.Организация КАК Организация,
	|	ДоговорыКонтрагентов.Владелец КАК Контрагент
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО РеализацияТоваровУслуг.Договор = ДоговорыКонтрагентов.Ссылка
	|		И РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка ЕСТЬ NULL
	|	И ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбоненскоеОбслуживание)
	|	И ДоговорыКонтрагентов.ВКМ_ДатаНачалаДействия < &ДатаОкончания
	|	И ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДействия > &ДатаНачала
	|	И НЕ ДоговорыКонтрагентов.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	КоличествоДокументов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		НовыйДокумент = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		НовыйДокумент.Заполнить(НовыйДокумент);
		НовыйДокумент.Дата = ДатаОкончания;
		НовыйДокумент.Организация = Выборка.Организация;
		НовыйДокумент.Контрагент = Выборка.Контрагент;
		НовыйДокумент.Договор = Выборка.Договор;
		НовыйДокумент.ВКМ_ВыполнитьАвтозаполнение();
		ДокументЗаполнен = НовыйДокумент.ПроверитьЗаполнение();
		
		Если ДокументЗаполнен = Ложь Тогда
			Возврат "Отказ";
		КонецЕсли;
		
		НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
		
		КоличествоДокументов = КоличествоДокументов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоДокументов;
	
КонецФункции

Процедура ПерезаполнитьРеализации(ДатаНачала, ДатаОкончания)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокументОбъект.Услуги.Очистить();
		ДокументОбъект.ВКМ_ВыполнитьАвтозаполнение();
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти