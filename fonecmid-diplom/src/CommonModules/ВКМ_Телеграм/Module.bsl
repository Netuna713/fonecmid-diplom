#Область СлужебныеПроцедурыИФункции

Функция ОтправитьСообщениеВТелеграм(ТекстСообщения) Экспорт
	
	Токен = Константы.ВКМ_ТокенУправленияТелеграмБотом.Получить();
	Сервер = "api.telegram.org";
	Порт = 443;
	Ресурс = "/bot" + Токен + "/sendMessage";
	
	ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL();
	HTTPСоединение = Новый HTTPСоединение(Сервер, Порт, , , , , ЗащищенноеСоединение);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	
	ДанныеСообщения = Новый Структура;
	ДанныеСообщения.Вставить("chat_id", Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить());
	ДанныеСообщения.Вставить("text", ТекстСообщения);
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, ДанныеСообщения);
	JSONСтрока = ЗаписьJSON.Закрыть();
	
	HTTPЗапрос = Новый HTTPЗапрос(Ресурс, Заголовки);
	HTTPЗапрос.УстановитьТелоИзСтроки(JSONСтрока);
	
	HTTPОтвет = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);
	
	Если HTTPОтвет.КодСостояния <> 200 Тогда
		ТекстОшибки = HTTPОтвет.ПолучитьТелоКакСтроку();
		ЗаписьЖурналаРегистрации("Telegram.ОшибкаОтправки", УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
	КонецЕсли;
	
	Возврат HTTPОтвет.КодСостояния
		
КонецФункции

#КонецОбласти